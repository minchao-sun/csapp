#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;
	iaddq $-3, %rdx		# len -= 3
	jge Loop			# if len >= 0, goto Loop
	jmp Rest
Loop:						# use Loop Unrolling
	mrmovq (%rdi), %r10		# read val1 from src...
	mrmovq 8(%rdi), %r11	# read val2 from src+8...
	mrmovq 16(%rdi), %r12	# read val3 from src+16...
	rmmovq %r10, (%rsi)		# store val1 to dst
	rmmovq %r11, 8(%rsi)	# store val2 to dst+8
	rmmovq %r12, 16(%rsi)	# store val3 to dst+16
	andq %r10, %r10		# val1 <= 0?
	jle Test2			# if so, goto Test2:
	iaddq $1, %rax		# count++
Test2:
	andq %r11, %r11		# val2 <= 0?
	jle Test3			# if so, goto Npos:
	iaddq $1, %rax		# count++
Test3:
	andq %r12, %r12		# val3 <= 0?
	jle Npos			# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos:
	iaddq $24, %rdi		# src += 3
	iaddq $24, %rsi		# dst += 3
	iaddq $-3, %rdx		# len -= 3
	jge Loop			# if len >= 0, goto Loop:
Rest:
	iaddq $2, %rdx		# len += 2 (could be: -1, 0, 1)
	jle Rest1			# if len <= 0, goto Rest1:
	mrmovq (%rdi), %r10		# read val1 from src...
	mrmovq 8(%rdi), %r11	# read val2 from src+8...
	rmmovq %r10, (%rsi)		# store val1 to dst
	rmmovq %r11, 8(%rsi)	# store val2 to dst+8
	andq %r10, %r10		# val1 <= 0?
	jle Test4			# if so, goto Test4:
	iaddq $1, %rax		# count++
Test4:
	andq %r11, %r11		# val2 <= 0?
	jle Done			# if so, goto Done:
	iaddq $1, %rax		# count++
	ret
Rest1:
	jl Done
	mrmovq (%rdi), %r10	# read val from src...
	rmmovq %r10, (%rsi)	# ...and store it to dst
	andq %r10, %r10		# val <= 0?
	jle Done
	iaddq $1, %rax		# count++
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
